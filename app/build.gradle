apply plugin: 'com.android.application'
apply plugin: 'android-apt'

def getRevision = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "net.myacxy.agsm"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName "0.1-" + getRevision()
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

apt {
    arguments {
        androidManifestFile variant.outputs[0].processResources.manifestFile
        resourcePackageName 'net.myacxy.agsm'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    apt 'org.androidannotations:androidannotations:3.3.2'
    compile 'org.androidannotations:androidannotations-api:3.3.2'

    // Google Support libraries
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:cardview-v7:22.2.1'
    compile 'com.android.support:design:22.2.1'
    compile 'com.android.support:recyclerview-v7:22.2.1'
    compile 'com.android.support:support-v4:22.2.1'

    // ORM database
    compile 'com.michaelpardo:activeandroid:3.1.0-SNAPSHOT'

    // Material Design helpers
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile 'com.github.clans:fab:1.5.5'
    compile('com.mikepenz:materialdrawer:3.1.2@aar') {
        transitive = true
    }
    compile ('com.github.ganfra:material-spinner:1.1.0'){
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }

    // FontAwesome + Material Design Icons
    compile 'com.mikepenz:iconics:1.6.2@aar'

    compile 'joda-time:joda-time:2.3'

    // Lorem ipsum placeholders
    compile 'com.thedeanda:lorem:1.2'

    // Testing
    testCompile "org.mockito:mockito-core:1.6"
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'com.jayway.android.robotium:robotium-solo:5.0.1'
}
